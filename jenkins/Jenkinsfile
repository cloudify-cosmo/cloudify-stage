def getMainBranch(String branch){
  if (branch =~ /.+-build/)
    return branch
  else
    return 'master'
}

def secrets = [
  [path: 'secret/jenkins/maps', engineVersion: 2, secretValues: [
    [envVar: 'MAPS_ACCESS_TOKEN', vaultKey: 'access_token']]],
]

def configuration = [vaultUrl: "${env.VAULT_URL}",  vaultCredentialId: 'vault-app-role', engineVersion: 2]

@Library('pipeline-shared-library') _

pipeline {
  agent {
    kubernetes {
      defaultContainer 'jnlp'
      yamlFile 'jenkins/build-pod.yaml'
    }
  }

  options {
    checkoutToSubdirectory('cloudify-stage')
    buildDiscarder(logRotator(numToKeepStr:'30'))
    timeout(time: 120, unit: 'MINUTES')
    timestamps()
  }

  environment {
    BRANCH = "${env.BRANCH_NAME}"
    WORKSPACE = "${env.WORKSPACE}"
    STAGE_DIR = "cloudify-stage"
    VERSION = getVersion("cloudify-manager","${env.BRANCH}").trim()
    PRERELEASE = getPreRelease("cloudify-manager","${env.BRANCH}").trim()
    CFY_NODE_RPM = 'https://cloudify-release-eu.s3.eu-west-1.amazonaws.com/cloudify/components/nodejs-16.16.0-1nodesource.x86_64.rpm'
    MAIN_BRANCH = getMainBranch("${env.BRANCH}")
    S3_BASE_URL = "${env.VERSION}/${env.PRERELEASE}-build/${env.STAGE_DIR}/${env.BRANCH}"
    S3_BUILD_PATH = "${env.S3_BASE_URL}/${env.BUILD_NUMBER}"
    S3_LATEST_BUILD_PATH = "${env.S3_BASE_URL}/latest"
  }

  stages {
    stage('Install required packages'){
      steps {
        container('node'){
          sh "rm -rf /var/lib/apt/lists/* &&  rm /etc/apt/sources.list.d/google-chrome.list && ls -la /usr/share/keyrings/ && sed -i '/dl.google.com/d' /etc/apt/sources.list && echo 'deb [trusted=yes] https://dl.google.com/linux/chrome/deb/ stable main' | tee -a /etc/apt/sources.list && apt update && apt-get -y install postgresql-client jq git"
          echo "Set maps access token"
          dir("${STAGE_DIR}/conf") {
            withVault([configuration: configuration, vaultSecrets: secrets]) {
              sh '''
                jq ".maps.accessToken = \\"$MAPS_ACCESS_TOKEN\\"" app.json > app.json.tmp && mv app.json.tmp app.json
              '''
            }
          }
          // Workaround for https://github.com/cypress-io/cypress/issues/20286
          echo "Fix directory ownership"
          dir("${STAGE_DIR}") {
            sh "chown -R `whoami` ."
          }
        }
      }
    }
    stage('Prepare & Audit main branch'){
      parallel{
        stage('Audit'){
          steps {
            catchError(buildResult: 'UNSTABLE', stageResult: 'FAILURE'){
              container('node'){
                dir("${STAGE_DIR}") {
                  echo 'Run npm production dependencies security audit'
                  sh 'npm run audit'
                }
              }
            }
          }
        }
        stage('Prepare & Run first test group'){
          steps {
            container('node'){
              dir("${STAGE_DIR}") {
                restoreArchiveFolder('~/.npm', "${WORKSPACE}/${STAGE_DIR}/package-lock.json ${WORKSPACE}/${STAGE_DIR}/backend/package-lock.json", 'stage-mb', 'npm_dir', '/mnt/data')
                echo "Install dependencies"
                sh 'npm run beforebuild'
                archiveFolder('~/.npm', "${WORKSPACE}/${STAGE_DIR}/package-lock.json ${WORKSPACE}/${STAGE_DIR}/backend/package-lock.json", 'stage-mb', 'npm_dir', '/mnt/data')
                echo "Build application"
                sh 'npm run build'
              }
              echo 'Prepare test folder'
              sh "mkdir -p ${WORKSPACE}/test && cp -rf ${WORKSPACE}/${STAGE_DIR}/. ${WORKSPACE}/test"
              dir("${WORKSPACE}/test"){
                echo 'Run static code analysis and code style check'
                sh 'npm run lint'
                echo 'Validate JSON files'
                sh 'npm run test:json'
              }
            }
          }
        }
        stage('Build RPM'){
          when {
            branch pattern: ".+-build|master", comparator: "REGEXP"
          }
          steps {
            container('rpmbuild'){
              echo 'Create RPM directory and copy all data from build stage to, for rpmbuild to run'
              sh """
                cd ~/rpmbuild
                mkdir -p BUILD && cp -rf ${WORKSPACE}/${STAGE_DIR}/. BUILD && cd BUILD
              """

              echo 'Check for cache'
              restoreArchiveFolder('~/.npm', "${WORKSPACE}/${STAGE_DIR}/package-lock.json ${WORKSPACE}/${STAGE_DIR}/backend/package-lock.json", 'stage-mb', 'npm_dir', '/mnt/data')

              echo 'Install NodeJS'
              sh "yum install -y ${CFY_NODE_RPM}"

              echo 'Lint RPM spec file & Install build dependencies for RPM spec file'
              sh '''
                cd ~/rpmbuild/BUILD
                rpmlint --info packaging/cloudify-stage.spec
                for i in {1..10}; do yum-builddep -y packaging/cloudify-stage.spec && break || sleep 5; done
              '''

              echo 'Download sources for RPM spec file & Build RPM'
              sh """
                cd ~/rpmbuild/BUILD
                spectool \
                  -d "CLOUDIFY_VERSION ${env.VERSION}" \
                  -d "CLOUDIFY_PACKAGE_RELEASE ${env.PRERELEASE}" \
                  -P packaging/cloudify-stage.spec \
                  -S -g

                rpmbuild \
                  -D "CLOUDIFY_VERSION ${env.VERSION}" \
                  -D "CLOUDIFY_PACKAGE_RELEASE ${env.PRERELEASE}" \
                  -bb packaging/cloudify-stage.spec
              """

              echo 'Save Artifacts'
              sh("mkdir -p ${WORKSPACE}/rpm && cp -rf /root/rpmbuild/RPMS/x86_64/. ${WORKSPACE}/rpm")
              dir("${WORKSPACE}/rpm") {
                archiveArtifacts artifacts: '*.rpm', onlyIfSuccessful: true
              }
            }
          }
          post {
            success {
              echo 'Uploading Artifacts'
              uploadToReleaseS3("${env.WORKSPACE}/rpm/","${env.S3_BUILD_PATH}")
              uploadToReleaseS3("${env.WORKSPACE}/rpm/","${env.S3_LATEST_BUILD_PATH}")
            }
          }
        }
      }
    }
    stage('Run second tests group') {
      parallel {
        stage('Test frontend unit'){
          steps{
            container('node'){
              dir("${WORKSPACE}/test"){
                echo 'Run frontend unit tests'
                sh 'npm run test:frontend:unit'
              }
            }
          }
        }
        stage('Test frontend components'){
          steps{
            container('node'){
              dir("${WORKSPACE}/test"){
                echo 'Run frontend component tests'
                sh 'npm run test:frontend:components'
              }
            }
          }
        }
        stage('Test backend'){
          steps{
            container('node'){
              dir("${WORKSPACE}/test"){
                echo 'Run backend tests'
                sh 'echo \'{"app":{"ssl":{"ca":null},"logLevelConf":"","logLevel":"debug"}}\' > conf/me.json'
                sh 'npm run test:backend'
                echo 'Verify backend start'
                sh '''
                  cd backend
                  npm run db-migrate
                  npm run start &
                  npm run wait-on-server
                '''
              }
            }
          }
        }
        stage('Size & check-types') {
          steps {
            container('node'){
              sh "mkdir -p ${WORKSPACE}/size && cp -rf ${WORKSPACE}/${STAGE_DIR}/. ${WORKSPACE}/size"
              dir("size") {
                echo 'Check bundle size'
                sh 'npm run size'
                echo 'Check TypeScript types'
                sh 'npm run check-types'
              }
              sh "rm -rf ${WORKSPACE}/size"
            }
          }
        }
        stage('Docs'){
          steps {
            container('node'){
              sh "mkdir -p ${WORKSPACE}/docs && cp -rf ${WORKSPACE}/${STAGE_DIR}/. ${WORKSPACE}/docs"
              dir("docs") {
                echo 'Generate widgets documentation'
                sh 'npm run docWidgets'

                echo 'Check if widgets documentation is up to date'
                sh 'git diff --exit-code ./widgets'

                echo 'Check if cloudify-ui-components version is the same as in official documentation'
                sh 'npm run docCheck'
              }
            }
          }
        }
        stage('Run Stage-UI System-tests') {
          when {
            branch pattern: ".+-build|master", comparator: "REGEXP"
          }
          steps {
            echo 'Trigger Stage-UI System-tests'
            build(job: 'Stage-UI-System-Test', parameters: [
              string(name: 'BRANCH', value: "${env.MAIN_BRANCH}"),
              string(name: 'STAGE_BRANCH', value: "${BRANCH}")
            ])
          }
        }
      }
    }
  }
}
